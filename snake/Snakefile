
'''Snakefile for Stivers HIV'''

__author__ = 'Jay Hesselberth <jay.hesselberth@gmail.com>'

from datetime import date
TODAY = date.today().isoformat()
workdir: 'results-stivers-hiv'

STRANDS = {'pos':'-strand +', 'neg':'-strand -'}

FASTA_HIV = config['FASTA_HIV']
FASTA_HIV_IDX = config['FASTA_HIV_IDX']
FASTA_FULL = config['FASTA_FULL']
FASTA_FULL_IDX = config['FASTA_FULL_IDX']
BWAIDX_HIV = config['BWAIDX_HIV']
BWAIDX_FULL = config['BWAIDX_FULL']
CHROMSIZE = config['CHROMSIZE']

from collections import namedtuple
sample_info = namedtuple('sample_info', ['description', 'url'])
SAMPLE_INFO = {id:sample_info(description, url)
               for id, description, url in
               zip(config['samples'], config['descriptions'],
                   config['urls'])}

shell.prefix("set -o nounset -o pipefail -o errexit -x; ")

rule all:
    input:
        expand('raw/{sample}.sra', sample=config['samples']),
        expand(('raw/{sample}_1.fastq.gz', 'raw/{sample}_2.fastq.gz'),
               sample=config['samples']),
        expand('alignment/{sample}/{sample}.bam', sample=config['samples']),
        expand('coverage/{sample}/{sample}.{strand}.bg.gz',
               sample=config['samples'],
               strand=STRANDS),
        expand('alignment/{sample}/{sample}.chimeras.bam',
               sample=config['samples']),
        expand('alignment/{sample}/{sample}.discordants.bam',
               sample=config['samples']),
        expand('alignment/{sample}/{sample}.chimeras.rmdup.bam',
               sample=config['samples']),
        expand('alignment/{sample}/{sample}.discordants.rmdup.bam',
               sample=config['samples']),
        expand('variants/{sample}.tidy.vcf.gz',
               sample=config['samples']),

include: 'rules/fastq.snake'
include: 'rules/alignment.snake'
include: 'rules/coverage.snake'
include: 'rules/variants.snake'

